apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'koin'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    Properties properties = new Properties()
    def keyProp = file('production.properties')
    properties.load(keyProp.newDataInputStream())
    def prop_keyAlias = new String(properties.getProperty('keyAlias'))
    def prop_keyPassword = new String(properties.getProperty('keyStorePassword'))
    def prop_storeFile = file(new String(properties.getProperty('keyStore')))

    signingConfigs {
        config {
            keyAlias 'dev_key'
            keyPassword 'dev_key'
            storeFile file('dev_key.jks')
            storePassword 'dev_key'
        }
        release {
            keyAlias prop_keyAlias
            keyPassword prop_keyPassword
            storeFile prop_storeFile
            storePassword prop_keyPassword
        }
    }
    compileSdkVersion 32
    defaultConfig {
        applicationId "com.antonio.samir.meteoritelandingsspots"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 15
        versionName "1.4.3"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.config
            versionNameSuffix '-dev'
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    buildFeatures {
        viewBinding true
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha3', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Architecture Components
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidx_lifecycle_version"

    implementation 'com.google.android.material:material:1.2.1'

    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    //Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$androidx_navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$androidx_navigation_version"


    //Paging
    implementation "androidx.paging:paging-runtime-ktx:2.1.2"

    //Room
    implementation "androidx.room:room-runtime:$androidx_room_version"
    kapt "androidx.room:room-compiler:$androidx_room_version"
    implementation "androidx.room:room-ktx:$androidx_room_version"

    //Play services
    implementation "com.google.android.gms:play-services-maps:$map_version"

    //insert-koin.io
    implementation "org.koin:koin-androidx-scope:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    implementation "org.koin:koin-androidx-fragment:$koin_version"
    // Koin for Android
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-android-scope:$koin_version"
    implementation "org.koin:koin-android-viewmodel:$koin_version"

    //Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    implementation 'com.google.code.gson:gson:2.8.6'

    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'

    implementation 'org.apache.commons:commons-lang3:3.11'

    //Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'

    implementation 'com.akexorcist:round-corner-progress-bar:2.1.0'

    //Stetho
    debugImplementation 'com.facebook.stetho:stetho:1.5.1'
    debugImplementation 'com.facebook.stetho:stetho-okhttp:1.5.1'
    debugImplementation 'com.facebook.stetho:stetho-urlconnection:1.5.1'

    //Nodle
    implementation 'io.nodle:nodlesdk-rc-lg:cbe8a42b18'

    implementation 'com.vanniktech:rxpermission:0.7.0'

    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.0-alpha3', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }

    //Test
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit:1.4.21'
    testImplementation 'org.assertj:assertj-core:3.18.1'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation "android.arch.core:core-testing:1.1.1"
    testImplementation 'com.flextrade.jfixture:jfixture:2.7.2'

    // testImplementation for pure JVM unit tests
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.9"
    // androidTestImplementation for Android instrumentation tests
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.9"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

}

apply plugin: 'com.google.gms.google-services'
